#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <ArduinoJson.h>
#include "freertos/FreeRTOS.h"
#include "freertos/task.h"

#include <ArduinoWebsockets.h>
#include <WiFi.h>
#include <HTTPClient.h>
#include <WebServer.h>
#include <EEPROM.h>
#include <BLEDevice.h>
#include <BLEUtils.h>
#include <BLEServer.h>
#include <BLE2902.h>
#include <queue>

#define SERVICE_UUID        "12345678-1234-1234-1234-1234567890ab"
#define CHARACTERISTIC_UUID "abcdefab-1234-5678-1234-abcdefabcdef"

BLECharacteristic *pCharacteristic;
BLEServer *pServer;
BLEAdvertising *pAdvertising;

LiquidCrystal_I2C lcd(0x38, 16, 2); 

bool deviceConnected = false;
bool dataReceivedFlag = false;
String connectedMac = "";
String targetMac = "";
uint16_t lastConnId = 0;

String jsonBuffer = "";
String latestJsonData = "";

std::vector<std::string> jsonChunks;
const int CHUNK_SIZE = 20;

std::queue<std::pair<String, std::vector<std::string>>> jsonQueue;

std::map<String, String> macNameMap = {
  {"01", "D0:EF:76:34:75:4A"},
  {"02", "D0:EF:76:31:AA:B6"},
  {"03", "A0:A3:B3:1E:E6:4A"},
  {"04", "A0:A3:B3:1D:48:16"}
};

#define TIMEZONE "ICT-7"

TaskHandle_t wifiTaskHandle;
TaskHandle_t bleTaskHandle;
TaskHandle_t webSocketTaskHandle;
void WiFiTask(void *pvParameters);
void WebSocketTask(void *pvParameters);
void BLETask(void *pvParameters);

String ID = "";
String Product = "";
String Address = "";
int Price ;
int Sale ;
String Time ="";
int new_price;
String saleStartTime ="";
String saleEndTime ="";


#define LED_XANH 22  // Ch√¢n LED Xanh
#define LED_DO 21    // Ch√¢n LED ƒê·ªè
#define BUTTON_AP 18 // Ch√¢n GPIO c·ªßa n√∫t nh·∫•n chuyen sang AP

#define AP_SSID "BLE_GATEWAY"
#define AP_PASSWORD "12345678"
const char* ws_server = "ws://192.168.0.103:8080"; // ƒê·ªïi theo ƒë·ªãa ch·ªâ WebSocket ServeR


#define EEPROM_SIZE 256
bool bleTaskStarted = false;

WebServer server(80);
using namespace websockets;
WebsocketsClient client;
String lastData ;

void reconnectWebSocket() {
    int retry = 0;
    client.close(); // ƒê·∫£m b·∫£o WebSocket ƒë∆∞·ª£c ƒë√≥ng tr∆∞·ªõc khi k·∫øt n·ªëi l·∫°i

    while (!client.connect(ws_server)) {
        Serial.printf("Th·ª≠ l·∫°i k·∫øt n·ªëi WebSocket l·∫ßn %d...\n", retry + 1);
        retry++;
        if (retry > 20) {
            Serial.println("Kh√¥ng th·ªÉ k·∫øt n·ªëi WebSocket , b·ªè qua.");
            lcd.clear();
            lcd.setCursor(0, 0); 
            lcd.print("Unable to connect to WebSocket");
            return;
        }
        vTaskDelay(pdMS_TO_TICKS(5000));
    }
    Serial.println("WebSocket ƒë√£ k·∫øt n·ªëi l·∫°i!");
    client.send("{\"device\":\"ESP32\"}"); // G·ª≠i ƒëƒÉng k√Ω l·∫°i sau khi k·∫øt n·ªëi
}

void setup() {
    Serial.begin(115200);
    Wire.begin(17, 16);
    lcd.begin(16, 2);
    lcd.backlight();
    lcd.setCursor(0, 0);  // ƒê·∫∑t con tr·ªè ·ªü d√≤ng 0, c·ªôt 0
    lcd.print("BLE GATEWAY");
    pinMode(LED_XANH, OUTPUT);
    pinMode(LED_DO, OUTPUT);
    // digitalWrite(LED_XANH, LOW);
    // digitalWrite(LED_DO, LOW);
    pinMode(BUTTON_AP, INPUT_PULLUP); 

    // Ch·ªâ ch·∫°y WebSocketTask khi WiFi ƒë√£ k·∫øt n·ªëi
    xTaskCreatePinnedToCore(WiFiTask, "WiFiTask", 4096, NULL, 2, &wifiTaskHandle, 1);
    xTaskCreatePinnedToCore(BLETask, "BLETask", 4096, NULL, 1, &bleTaskHandle, 0);
}

// H√†m l·∫•y v√† hi·ªÉn th·ªã th·ªùi gian hi·ªán t·∫°i
void printLocalTime() {
    struct tm timeinfo;
    if (!getLocalTime(&timeinfo)) {
        Serial.println("Kh√¥ng th·ªÉ l·∫•y th·ªùi gian SNTP");
        return;
    }
    Serial.printf("Th·ªùi gian hi·ªán t·∫°i: %02d:%02d:%02d %02d/%02d/%04d\n",
                  timeinfo.tm_hour, timeinfo.tm_min, timeinfo.tm_sec,
                  timeinfo.tm_mday, timeinfo.tm_mon + 1, timeinfo.tm_year + 1900);
}

// Task ƒë·ªìng b·ªô th·ªùi gian qua SNTP
void SNTPTask(void *pvParameters) {
    Serial.println("ƒêang ƒë·ªìng b·ªô th·ªùi gian SNTP...");
    configTime(7 * 3600, 0, "pool.ntp.org", "time.nist.gov");
    vTaskDelay(pdMS_TO_TICKS(5000)); // ƒê·ª£i 5 gi√¢y ƒë·ªÉ ƒë·ªìng b·ªô
    while (true) {
        printLocalTime();  // In th·ªùi gian hi·ªán t·∫°i
        vTaskDelay(pdMS_TO_TICKS(3600000)); // C·∫≠p nh·∫≠t l·∫°i sau 1 gi·ªù (3600 gi√¢y)
    }
}

//Xu ly viec doc ghi Wifi t∆∞ EEPROM
void saveWiFiCredentials(const String& ssid, const String& password, const String& wsip) {
    EEPROM.begin(EEPROM_SIZE);
    EEPROM.writeString(0, ssid);
    EEPROM.writeString(64, password);
    EEPROM.writeString(128, wsip);
    EEPROM.commit();
    EEPROM.end();
}

void loadWiFiCredentials(String& ssid, String& password, String& wsip) {
    EEPROM.begin(EEPROM_SIZE);
    ssid = EEPROM.readString(0);
    password = EEPROM.readString(64);
    wsip = EEPROM.readString(128);
    EEPROM.end();
}

// h√†m x√≥a(chauw s·ª≠ d·ª•ng)
void clearWiFiCredentials() {
    EEPROM.begin(EEPROM_SIZE);
    for (int i = 0; i < EEPROM_SIZE; i++) {
        EEPROM.write(i, 0xFF);  // Ghi gi√° tr·ªã 0xFF v√†o to√†n b·ªô b·ªô nh·ªõ EEPROM
    }
    EEPROM.commit();
    EEPROM.end();
}

bool checkInternet() {
    HTTPClient http;
    http.begin("http://clients3.google.com/generate_204");
    int httpCode = http.GET();
    http.end();
    return (httpCode == 204);
}
std::vector<std::string> splitJson(String input, int chunkSize) {
  std::vector<std::string> chunks;
  for (int i = 0; i < input.length(); i += chunkSize) {
    String part = input.substring(i, i + chunkSize);
    chunks.push_back(part.c_str());
  }
  return chunks;
}


void handleReceivedData(String data) {
  latestJsonData = data;

  // T√¨m ID t∆∞∆°ng ·ª©ng v·ªõi connectedMac
  String id = "";
  for (auto& pair : macNameMap) {
    if (pair.second.equalsIgnoreCase(connectedMac)) {
      id = pair.first;
      break;
    }
  }
  // Th√™m "id" v√†o ƒë·∫ßu JSON n·∫øu c√≥ ID
  if (id != "" && latestJsonData.startsWith("{") && latestJsonData.endsWith("}")) {
    latestJsonData.remove(0, 1);  // Xo√° d·∫•u {
    latestJsonData.remove(latestJsonData.length() - 1);  // Xo√° d·∫•u }
    latestJsonData = "{\"id\":\"" + id + "\"," + latestJsonData + "}";  // Gh√©p l·∫°i v·ªõi id ·ªü ƒë·∫ßu
  }

  Serial.println("‚úÖ Received full JSON from client (with id on top):");
  Serial.println(latestJsonData);
  if (client.send(latestJsonData)) {
    Serial.println("Sent JSON to server:");
    Serial.println(latestJsonData);
    delay(100);  // Th√™m m·ªôt kho·∫£ng th·ªùi gian ch·ªù (100ms) sau m·ªói l·∫ßn g·ª≠i d·ªØ li·ªáu
  } else {
    Serial.println("Error sending data to WebSocket server.");
  }
}

class MyServerCallbacks : public BLEServerCallbacks {
  void onConnect(BLEServer* server, esp_ble_gatts_cb_param_t *param) override {
    char macStr[18];
    snprintf(macStr, sizeof(macStr), "%02X:%02X:%02X:%02X:%02X:%02X",
             param->connect.remote_bda[0], param->connect.remote_bda[1],
             param->connect.remote_bda[2], param->connect.remote_bda[3],
             param->connect.remote_bda[4], param->connect.remote_bda[5]);
    connectedMac = String(macStr);
    connectedMac.toUpperCase();
    lastConnId = param->connect.conn_id;

    Serial.print("Client attempting to connect: ");
    Serial.println(connectedMac);

    if (connectedMac != targetMac) {
      Serial.println("Not target MAC, disconnecting...");
      vTaskDelay(50 / portTICK_PERIOD_MS);
      server->disconnect(lastConnId);
    } else {
      deviceConnected = true;
      Serial.println("Client accepted.");
      lcd.clear();
      lcd.setCursor(0, 0); 
      lcd.print("Client accepted");
      delay(500);
    }
    dataReceivedFlag = false;
  }

  void onDisconnect(BLEServer* pServer) override {
    Serial.println("Client disconnected");
    deviceConnected = false;
    connectedMac = "";
    lastConnId = 0;
    // Ti·∫øp t·ª•c qu·∫£ng b√°
    pAdvertising->start();
    dataReceivedFlag = false;
  }

  void onWrite(BLECharacteristic *pCharacteristic) {
    String receivedData = pCharacteristic->getValue().c_str();  // Nh·∫≠n d·ªØ li·ªáu g·ª≠i t·ª´ client
    Serial.println("Data received from client:");
    Serial.println(receivedData);  // In ra serial d·ªØ li·ªáu nh·∫≠n ƒë∆∞·ª£c
  }
};

class MyCharacteristicCallbacks : public BLECharacteristicCallbacks {
  void onWrite(BLECharacteristic* pCharacteristic) override {
    String chunk = pCharacteristic->getValue().c_str();
    Serial.println("Received chunk:");
    Serial.println(chunk);

    jsonBuffer += chunk;

    if (chunk == "|END|" || jsonBuffer.endsWith("}")) {
      Serial.println("JSON complete, processing...");
      dataReceivedFlag = true;
      handleReceivedData(jsonBuffer);
      jsonBuffer = ""; // reset sau khi x·ª≠ l√Ω
    }
  }
};

void startAccessPoint() {
    WiFi.mode(WIFI_AP);
    WiFi.softAP(AP_SSID, AP_PASSWORD);
    Serial.println("AP Started. Connect and go to 192.168.4.1");
    lcd.clear();
    lcd.setCursor(0, 0); 
    lcd.print("AP Started");
    lcd.setCursor(0, 1); 
    lcd.print("IP: 192.168.4.1");

    server.on("/", HTTP_GET, []() {
        int n = WiFi.scanNetworks();
        String savedSSID, savedPassword, savedWSIP;
        loadWiFiCredentials(savedSSID, savedPassword, savedWSIP);

        String page = R"rawliteral(
            <html>
            <head>
                <meta charset='UTF-8'>
                <style>
                    body {
                        background: linear-gradient(135deg, #f3f4f6, #e3e4e8);
                        text-align: center;
                        font-family: Arial, sans-serif;
                        color: #333;
                    }
                    h2 {
                        font-size: 28px;
                        color: #222;
                    }
                    form {
                        background: white;
                        padding: 20px;
                        border-radius: 10px;
                        display: inline-block;
                        box-shadow: 0px 4px 10px rgba(0, 0, 0, 0.1);
                    }
                    select, input {
                        font-size: 18px;
                        margin: 10px;
                        padding: 8px;
                        border-radius: 5px;
                        border: 1px solid #ccc;
                        width: 90%;
                        max-width: 300px;
                    }
                    input[type='submit'] {
                        background: #4CAF50;
                        color: white;
                        border: none;
                        padding: 10px 20px;
                        cursor: pointer;
                        border-radius: 5px;
                    }
                    input[type='submit']:hover {
                        background: #45a049;
                    }
                    input:disabled {
                        background: #aaa;
                        cursor: not-allowed;
                    }
                    p {
                        color: red;
                        font-size: 16px;
                        margin-top: 5px;
                        display: none;
                    }
                </style>
                <script>
                    function validateForm() {
                        var password = document.getElementById('password').value;
                        var wsip = document.getElementById('wsip').value;
                        var submitBtn = document.getElementById('submitBtn');
                        var warningText = document.getElementById('warningText');
                        if (password.length >= 8 && wsip.length > 0) {
                            submitBtn.disabled = false;
                            warningText.style.display = 'none';
                        } else {
                            submitBtn.disabled = true;
                            warningText.style.display = 'block';
                        }
                    }
                    function togglePassword() {
                        var passwordField = document.getElementById('password');
                        var checkbox = document.getElementById('showPassword');
                        passwordField.type = checkbox.checked ? 'text' : 'password';
                    }
                </script>
            </head>
            <body>
                <h2>Ch·ªçn m·∫°ng WiFi v√† nh·∫≠p IP WebSocket</h2>
                <form action='/connect' method='POST'>
                    <select name='ssid'>
        )rawliteral";

        for (int i = 0; i < n; i++) {
            page += "<option value='" + WiFi.SSID(i) + "'>" + WiFi.SSID(i) + "</option>";
        }

        page += R"rawliteral(
                    </select><br>
                    <input id='password' name='password' type='password' placeholder='M·∫≠t kh·∫©u' oninput='validateForm()' required><br>
                    <input type='checkbox' id='showPassword' onclick='togglePassword()'> <label for='showPassword'>Hi·ªÉn th·ªã m·∫≠t kh·∫©u</label><br>
                    <input id='wsip' name='wsip' type='text' placeholder='WebSocket IP (VD: 192.168.4.1)' oninput='validateForm()' required><br>
                    <p id='warningText'>M·∫≠t kh·∫©u ph·∫£i >=8 k√Ω t·ª± v√† IP kh√¥ng ƒë∆∞·ª£c ƒë·ªÉ tr·ªëng!</p>
                    <input id='submitBtn' type='submit' value='K·∫øt n·ªëi' disabled>
                </form>
            </body>
            </html>
        )rawliteral";

        server.send(200, "text/html", page);
    });

    server.on("/connect", HTTP_POST, []() {
        String ssid = server.arg("ssid");
        String password = server.arg("password");
        String wsip = server.arg("wsip");

        saveWiFiCredentials(ssid, password, wsip);

        String response = R"rawliteral(
            <html>
            <head>
                <meta charset='UTF-8'>
                <style>
                    body {
                        text-align: center;
                        font-family: Arial, sans-serif;
                        color: #333;
                    }
                    h2 {
                        font-size: 24px;
                    }
                    button {
                        background: #007bff;
                        color: white;
                        border: none;
                        padding: 10px 20px;
                        font-size: 16px;
                        border-radius: 5px;
                        cursor: pointer;
                        margin-top: 20px;
                    }
                    button:hover {
                        background: #0056b3;
                    }
                </style>
            </head>
            <body>
                <h2>ƒê√£ l∆∞u! ƒêang kh·ªüi ƒë·ªông l·∫°i ESP32...</h2>
                <form action="/" method="GET">
                    <button type="submit">Quay l·∫°i c·∫•u h√¨nh</button>
                </form>
            </body>
            </html>
        )rawliteral";

        server.send(200, "text/html", response);

        vTaskDelay(3000 / portTICK_PERIOD_MS);
        ESP.restart();
    });

    server.begin();
}


void WiFiTask(void *parameter) {
  String ssid, password, WSIP;

loadWiFiCredentials(ssid, password, WSIP);

    if (ssid == "" || digitalRead(BUTTON_AP) == LOW) {  
        Serial.println("No WiFi. Switch to AP...");
        lcd.clear();
        lcd.setCursor(0, 0); 
        lcd.print("No WiFi.");
        lcd.setCursor(0, 1); 
        lcd.print("Switch to AP...");

    if (bleTaskHandle != NULL) {
        Serial.println("Stop BLE...");
        vTaskDelete(bleTaskHandle);
        bleTaskHandle = NULL;
    }
        startAccessPoint();
        while (true) {
            server.handleClient();
            vTaskDelay(10 / portTICK_PERIOD_MS);
        }
    }

    WiFi.begin(ssid.c_str(), password.c_str());
    Serial.print("Connecting to WiFi");
    lcd.clear();
    lcd.setCursor(0, 0); 
    lcd.print("Connecting to WF");
    lcd.setCursor(0, 1); 
    lcd.print(".....");
    for (int i = 0; i < 20; i++) {
        if (WiFi.status() == WL_CONNECTED) break;
        Serial.print(".");
        vTaskDelay(500 / portTICK_PERIOD_MS);
    }

    if (WiFi.status() == WL_CONNECTED) {
        Serial.println("\nWiFi connected!");
        Serial.print("IP Address: ");
        Serial.println(WiFi.localIP());
        lcd.clear();
        lcd.setCursor(0, 0); 
        lcd.print("IP Address:");
        lcd.setCursor(0, 1); 
        lcd.print(WiFi.localIP());
        digitalWrite(LED_XANH, HIGH);

        if (!checkInternet()) {
            Serial.println("No Internet! Keep AP active.");
            startAccessPoint();
            while (!checkInternet()) {
                server.handleClient();
                vTaskDelay(1000 / portTICK_PERIOD_MS);
            }
        }

        Serial.println("Internet connected! Turn off AP.");
        WiFi.softAPdisconnect(true);

        if (checkInternet()) {
            Serial.println("Start WebSocket Task...");
            xTaskCreate(WebSocketTask, "WebSocketTask", 8192, NULL, 2, &webSocketTaskHandle);
            xTaskCreate(SNTPTask, "SNTP Task", 4096, NULL, 1, NULL);
        } else {
            Serial.println("No Internet, WebSocket Task not started.");
        }

    } else {
        Serial.println("WiFi connection failed! Switching to AP.");
        startAccessPoint();
    }

    vTaskDelete(NULL);
}


void onMessageCallback(WebsocketsMessage message) {
  String newData = message.data();
  // Ki·ªÉm tra xem d·ªØ li·ªáu c√≥ thay ƒë·ªïi kh√¥ng
  if (newData == lastData) {
      return;
  }
  lastData = newData; // C·∫≠p nh·∫≠t d·ªØ li·ªáu m·ªõi
  Serial.print("Nh·∫≠n d·ªØ li·ªáu WebSocket: ");
  Serial.println(newData); 

  // Chuy·ªÉn d·ªØ li·ªáu nh·∫≠n ƒë∆∞·ª£c th√†nh JSON
  StaticJsonDocument<256> doc; // Dung l∆∞·ª£ng ph√π h·ª£p v·ªõi JSON c·ªßa b·∫°n
  DeserializationError error = deserializeJson(doc, newData);

  if (error) {
      Serial.print("L·ªói ph√¢n t√≠ch JSON: ");
      Serial.println(error.c_str());
      return;
  }

  // Ki·ªÉm tra n·∫øu d·ªØ li·ªáu c√≥ gi√° tr·ªã 'null'
  if (doc["id"].isNull() || doc["product"].isNull() || doc["address"].isNull() ||
  doc["price"].isNull() || doc["sale"].isNull() || doc["saleStartTime"].isNull() || 
  doc["saleEndTime"].isNull()) {
      return;
  }

  // L·∫•y th·ªùi gian hi·ªán t·∫°i
  time_t now = time(NULL);
  struct tm *currentTime = localtime(&now);
  int currentSeconds = currentTime->tm_hour * 3600 + currentTime->tm_min * 60;
  
  // Chuy·ªÉn saleStartTime & saleEndTime th√†nh gi√¢y trong ng√†y
  String saleStartTime = doc["saleStartTime"].as<String>();
  String saleEndTime = doc["saleEndTime"].as<String>();

  struct tm saleStartStruct = {0}, saleEndStruct = {0};
  strptime(saleStartTime.c_str(), "%H:%M", &saleStartStruct);
  int startSeconds = saleStartStruct.tm_hour * 3600 + saleStartStruct.tm_min * 60;
  int endSeconds = 86400;  // M·∫∑c ƒë·ªãnh l√† h·∫øt ng√†y (23:59:59) n·∫øu saleEndTime r·ªóng
  if (saleEndTime != "") {
      strptime(saleEndTime.c_str(), "%H:%M", &saleEndStruct);
      endSeconds = saleEndStruct.tm_hour * 3600 + saleEndStruct.tm_min * 60;
  }

  // So s√°nh th·ªùi gian
  if (currentSeconds < startSeconds || currentSeconds > endSeconds) {
      doc["sale"] = "0";
  }

  // T·∫°o JSON m·ªõi v·ªõi d·ªØ li·ªáu c·∫ßn thi·∫øt
  StaticJsonDocument<256> filteredDoc;
  filteredDoc["id"] = doc["id"];
  filteredDoc["product"] = doc["product"];
  filteredDoc["price"] = doc["price"];
  filteredDoc["sale"] = doc["sale"];

  // Th√™m th·ªùi gian th·ª±c d·∫°ng HH:MM:SS DD/MM/YYYY
  // G·ª≠i JSON c·∫≠p nh·∫≠t
  String newJson;
  serializeJson(filteredDoc, newJson);

  if (newJson.startsWith("{") && newJson.endsWith("}")) {
    Serial.println("JSON input received:");
    Serial.println(newJson);

    // L·∫•y ID t·ª´ JSON
    int idPos = newJson.indexOf("\"id\":\"");
    if (idPos != -1) {
      int idStart = idPos + 6;
      int idEnd = newJson.indexOf("\"", idStart);
      String id = newJson.substring(idStart, idEnd);

      if (macNameMap.count(id)) {
        String mac = macNameMap[id];
        mac.toUpperCase();
        auto chunks = splitJson(newJson, CHUNK_SIZE);
        jsonQueue.push({mac, chunks});
        Serial.println("Queued JSON for ID: " + id + ", MAC: " + mac);
        
        if (targetMac != mac) {
            targetMac = mac;  // C·∫≠p nh·∫≠t targetMac v·ªõi MAC m·ªõi
            Serial.println("Target MAC updated to: " + targetMac);
        }
      } else {
        Serial.println("Unknown ID: " + id);
      }
    } else {
      Serial.println("Invalid JSON, no 'id' found.");
    }
  } else {
    Serial.println("Invalid input.");
  }
}

void onEventsCallback(WebsocketsEvent event, String data) {
    if (event == WebsocketsEvent::ConnectionOpened) {
        Serial.println("K·∫øt n·ªëi WebSocket th√†nh c√¥ng!");
        client.send("{\"device\":\"ESP32\"}"); // G·ª≠i ƒëƒÉng k√Ω khi k·∫øt n·ªëi th√†nh c√¥ng
    } else if (event == WebsocketsEvent::ConnectionClosed) {
        Serial.println("WebSocket b·ªã ƒë√≥ng, th·ª≠ k·∫øt n·ªëi l·∫°i...");
        reconnectWebSocket();
    }
}


void WebSocketTask(void *pvParameters) {
    client.onMessage(onMessageCallback);
    client.onEvent(onEventsCallback);
    reconnectWebSocket(); // K·∫øt n·ªëi ngay khi task b·∫Øt ƒë·∫ßu

    while (1) {
        if (WiFi.status() == WL_CONNECTED) {
            client.poll(); // Ki·ªÉm tra v√† nh·∫≠n d·ªØ li·ªáu WebSocket
        } else {
            Serial.println("M·∫•t k·∫øt n·ªëi WiFi, ƒë·ª£i 5s th·ª≠ l·∫°i...");
            vTaskDelay(pdMS_TO_TICKS(5000));
        }
        vTaskDelay(pdMS_TO_TICKS(50));  // Gi·∫£m th·ªùi gian ch·ªù ƒë·ªÉ nh·∫≠n d·ªØ li·ªáu nhanh h∆°n
    }
}
void processJsonQueue() {
    if (jsonQueue.empty()) {
      return;  // Kh√¥ng l√†m g√¨ n·∫øu h√†ng ƒë·ª£i tr·ªëng
    }
  
    // L·∫•y d·ªØ li·ªáu ƒë·∫ßu ti√™n t·ª´ h√†ng ƒë·ª£i
    auto& current = jsonQueue.front();
    String mac = current.first;
    auto chunks = current.second;
  
    // G√°n targetMac v·ªõi mac t·ª´ h√†ng ƒë·ª£i m√† kh√¥ng c·∫ßn g·ªçi l·∫°i onConnect
    if (mac != targetMac) {
      targetMac = mac;  // C·∫≠p nh·∫≠t targetMac
      Serial.println("Updated target MAC to: " + targetMac);
      lcd.clear();
      lcd.setCursor(0, 0); 
      lcd.print("Updated Data for");
      lcd.setCursor(0, 1); 
      lcd.print(targetMac);
    }
  
    // Ki·ªÉm tra n·∫øu ƒë√£ k·∫øt n·ªëi v√† b·∫Øt ƒë·∫ßu g·ª≠i d·ªØ li·ªáu
    if (deviceConnected && !jsonQueue.empty()) {
      delay(500);
      Serial.println("Chunks sent.");
      for (auto& chunk : chunks) {
        delay(100);
        pCharacteristic->setValue(chunk.c_str());
        pCharacteristic->notify();
        Serial.print("üì§ Sent: ");
        Serial.println(chunk.c_str());
        
      }
  
      // G·ª≠i k√Ω t·ª± k·∫øt th√∫c
      pCharacteristic->setValue("|END|");
      pCharacteristic->notify();
      Serial.println("‚úÖ All chunks sent.");
      lcd.clear();
      lcd.setCursor(0, 0); 
      lcd.print("Updated Success!");
      // X√≥a kh·ªèi h√†ng ƒë·ª£i
      jsonQueue.pop();
  
      delay(200);
      // Ng·∫Øt k·∫øt n·ªëi v√† reset targetMac sau khi ho√†n t·∫•t g·ª≠i
      Serial.println("üîå Disconnecting client...");
      pServer->disconnect(lastConnId);
      delay(1000);
      targetMac = "";  // Reset MAC sau khi ng·∫Øt k·∫øt n·ªëi
    }
}
void BLETask(void* parameter) {
  BLEDevice::init("ESP32_JSON_Server");

  pServer = BLEDevice::createServer();
  pServer->setCallbacks(new MyServerCallbacks());

  BLEService *pService = pServer->createService(SERVICE_UUID);
  pCharacteristic = pService->createCharacteristic(
                      CHARACTERISTIC_UUID,
                      BLECharacteristic::PROPERTY_READ | BLECharacteristic::PROPERTY_WRITE | BLECharacteristic::PROPERTY_NOTIFY
                    );
  pCharacteristic->addDescriptor(new BLE2902());
  pCharacteristic->setCallbacks(new MyCharacteristicCallbacks()); 
  pService->start();

  pAdvertising = BLEDevice::getAdvertising();
  pAdvertising->addServiceUUID(SERVICE_UUID);
  pAdvertising->start();

  Serial.println("üì° BLE Advertising started...");

  while (true) {
 
    vTaskDelay(2000 / portTICK_PERIOD_MS);
  }
}

void loop() {
    if (client.available()) {
        client.poll(); // Ki·ªÉm tra tin nh·∫Øn m·ªõi
        processJsonQueue();
    }
    server.handleClient();
}
